PROG -> DECLLIST PROCDECLLIST START ; STMTLIST END ;
DECLLIST -> ''
DECLLIST -> DECLSTMT ; DECLLIST
DECLSTMT -> DECLARE ( IDLIST ) DATATYPE
PROCDECLLIST -> ''
PROCDECLLIST -> PROCDECL PROCDECLLIST
PROCDECL -> id : PROCEDURE ( SUPERIDLIST ) ; STMTLIST END id ;
DATATYPE -> INTEGER
DATATYPE -> FLOAT
DATATYPE -> CHAR
DATATYPE -> ARRAYTYPE
ARRAYTYPE -> ARRAY [ E ] OF DATATYPE
STMTLIST -> ''
STMTLIST -> SUPERSTMT ; STMTLIST
SUPERSTMT -> LABELSTMT
SUPERSTMT -> IDLESSSTMT
LABELSTMT -> id POSTLABELSTMT
POSTLABELSTMT -> : STMT
POSTLABELSTMT -> ATTRSTMT
POSTLABELSTMT -> PROCSTMT
STMT -> LABELLESSSTMT
STMT -> IDLESSSTMT
LABELLESSSTMT -> id POSTLABELLESSSTMT
POSTLABELLESSSTMT -> ATTRSTMT 
POSTLABELLESSSTMT -> PROCSTMT
IDLESSSTMT -> STOPSTMT
IDLESSSTMT -> IOSTMT
IDLESSSTMT -> CONTROLSTMT
CONTROLSTMT -> IFSTMT
CONTROLSTMT -> GOTOSTMT
CONTROLSTMT -> LOOPSTMT
CONTROLSTMT -> EXITSTMT
IFSTMT -> IF E THEN STMTLIST ELSESTMT ENDIF
ELSESTMT -> ELSE STMTLIST
ELSESTMT -> ''
GOTOSTMT -> GOTO id
LOOPSTMT -> LOOP ; STMTLIST ENDLOOP
EXITSTMT -> EXITWHEN E
STOPSTMT -> STOP
ATTRSTMT -> := E
PROCSTMT -> ( EXPRLIST )
SUPERIDLIST -> ''
SUPERIDLIST -> IDLIST
IDLIST -> id IDLIST2
IDLIST2 -> , id IDLIST2
IDLIST2 -> ''
EXPRLIST -> ''
EXPRLIST -> E EXPRLISTTAIL
EXPRLISTTAIL -> , E EXPRLISTTAIL
EXPRLISTTAIL -> ''
E   -> T E'
E'  -> A E
E'  -> ''
A   -> &
A   -> |
T   -> T2 T'
T'  -> B T
T'  -> ''
B   -> >
B   -> <
B   -> =
B   -> !=
B   -> <=
B   -> >=
T2	-> T3 T2'
T2'	-> C T2
T2'	-> ''
C 	-> +
C 	-> -
T3	-> F T3'
T3'	-> D T3
T3'	-> ''
D 	-> *
D 	-> /
D 	-> %
F 	-> ! F
F 	-> - F
F 	-> ( F )
F 	-> id
F 	-> LITERAL
LITERAL -> integer
LITERAL -> float
LITERAL -> char
SKIPSTMT -> ''
SKIPSTMT -> SKIP
IOSTMT -> GET ( IDLIST )
IOSTMT -> PUT SKIPSTMT ( IDLIST )
